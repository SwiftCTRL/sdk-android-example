apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion commonCompileSdkVersion.toInteger()
    buildToolsVersion commonBuildToolsVersion

    defaultConfig {
        minSdkVersion commonMinSdkVersion.toInteger()
        targetSdkVersion commonTargetSdkVersion.toInteger()
        applicationId "com.swiftctrl.example"
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        viewBinding true
    }
    signingConfigs {
        config {
            keyAlias cert_key_alias
            keyPassword cert_key_password
            storeFile file(cert_keystore_path)
            storePassword cert_keystore_password
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.config
        }
    }
}
configurations {
    ktlint
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"


    ////////////////////////////////////////////////////////////////////////////////////////////////
    // SWIFTCTRL SDK !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    implementation "com.swiftctrl:swiftctrl-sdk:1.0.0"
    ////////////////////////////////////////////////////////////////////////////////////////////////

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // ANDROIDX
    implementation "androidx.core:core-ktx:$androidxCoreKtxVersion"
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.activity:activity-ktx:$androidxActivityVersion"
    implementation "androidx.fragment:fragment-ktx:$androidxFragmentKtxVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidxLifecycleKtxVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidxLifecycleKtxVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidxLifecycleKtxVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidxLifecycleKtxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$androidxConstraintLayoutVersion"

    // Material Design
    implementation "com.google.android.material:material:$materialAndroidVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'

    // Picasso
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$picassoOkHttpVersion"

    ////////////////////////////////////////////////////////////////////////////////////////////////
    // SUPPORT & DEBUG

    // JWT Token
    implementation 'com.auth0.android:jwtdecode:2.0.0'


    // Timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$canaryVersion"

    // KT lint
    ktlint "com.pinterest:ktlint:$ktlintVersion"

    // QuickLib
    implementation "com.quicklib:quicklib-core:$quicklibVersion"
    implementation "com.quicklib:quicklib-app:$quicklibVersion"

    // QR Codes
    // implementation "com.github.kenglxn.QRGen:javase:2.6.0"
    implementation "com.google.zxing:core:3.3.0"


    ////////////////////////////////////////////////////////////////////////////////////////////////
    // DEPLOY
    implementation "com.microsoft.appcenter:appcenter-analytics:3.3.1"
    implementation "com.microsoft.appcenter:appcenter-crashes:3.3.1"
    implementation "com.microsoft.appcenter:appcenter-distribute:3.3.1"



    ////////////////////////////////////////////////////////////////////////////////////////////////
    // TESTING

    // Integration
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Unit
    testImplementation 'junit:junit:4.12'
}

// KT lint tasks------------------------------------------------------------------------------------
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}